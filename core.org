#+property: tangle core.jo
#+title: core

* [todo-stack]

*** merge old core

*** (case) over class and data

* Int

  #+begin_src jojo
  (+jojo even? 2 mod 0 eq?)
  (+jojo odd? even? not)
  #+end_src

* List

*** List

    #+begin_src jojo
    (+union List
      <null>
      <cons>)
    #+end_src

*** list-length

    #+begin_src jojo
    (+jojo list-length (-> :list -- Int)
      (cond [:list null?] 0
            else [:list .cdr list-length inc]))
    #+end_src

*** list-ref

    #+begin_src jojo
    (+jojo list-ref (-> :list :index -- Value)
      (cond [:list null?]
            ["- list-ref fail"
             string-write newline
             "  index greater then length of list"
             string-write newline
             error]
            [:index 0 eq?] [:list .car]
            else [:list .cdr :index dec list-ref]))
    #+end_src

*** list-append

    #+begin_src jojo
    (+jojo list-append (-> :ante :succ -> List)
      (cond
        [:ante null?] [:succ]
        [:ante cons?] [:ante .cdr :succ list-append
                       :ante .car swap cons]))
    #+end_src

*** tail-cons

    #+begin_src jojo
    (+jojo tail-cons null cons list-append)
    #+end_src

* testing

*** (assert) & (assert!)

    #+begin_src jojo
    (+jojo assert (-> :body -- sexp)
      `(if (@ :body list-spread)
         {}
         {"- (assert) fail" string-write newline
          "  assertion : " string-write
          (quote (@ :body)) sexp-list-write newline}))

    (+jojo assert! (-> :body -- sexp)
      `(if (@ :body list-spread)
         {}
         {"- (assert!) fail" string-write newline
          "  assertion : " string-write
          (quote (@ :body)) sexp-list-write newline
          error}))
    #+end_src

* w -- gene

  #+begin_src jojo
  (+gene w (-> :x --) :x print)

  (+disp w (-> :list List --)
    (cond [:list null?] ["()" w]
          [:list cons?] ["(" w :list list-write/loop ")" w]))

  (+jojo list-write/loop (-> :list --)
    (cond
      [:list null?] []
      [:list .cdr null?] [:list .car w]
      else [:list .car w space
            :list .cdr list-write/loop]))
  #+end_src

* equal? -- gene

  #+begin_src jojo
  (+gene equal? (-> :x :y -- Bool) false)
  #+end_src
